{В модуле описаны две функции, которые проверяют правильность вводимого
 пользователем символа. Функция TrueIntKeys проверяет целые числа и имеет
 параметры: key - введенный пользователем символ; str - строка в TEdit;
 sign - если True, то число со знаком, если False - без знака. Если пользо-
 ватель ввел правильный символ, функция его не изменяет, иначе возвращает
 символ #0 - то есть, ничего. Функция TrueFloatKeys аналогичным образом
 делает проверку вещественных чисел. Пример использования:
 Key:= TrueIntKeys(Key, Edit1.Text, True); //целое со знаком
 Key:= TrueIntKeys(Key, Edit1.Text, False); //целое без знака
 Key:= TrueFloatKeys(Key, Edit1.Text); //вещественное
}

unit Numbers;

{$mode objfpc}{$H+}

interface

uses
  Classes, SysUtils;

//проверка правильности символа в целом числе:
function TrueIntKeys(key:char; str:string; sign:boolean):char;
//проверка правильности символа в вещественном числе:
function TrueFloatKeys(key:char; str:string):char;

implementation

{проверка правильности символа в целом числе}
function TrueIntKeys(key:char; str:string; sign:boolean):char;
begin
  //сначала укажем, что возвращается тот же символ, что ввел пользователь:
  Result:= key;
  //далее делаем проверку на правильность символа. если символ не правильный,
  //мы его запретим:
  case key of
    //все числа разрешаем:
    '0'..'9': ;
    //backspace разрешаем:
    #8: ;
    //если знаковое, то разрешаем минус при условии, что минус - первый
    //символ в строке:
    '-': if sign and (Length(str) = 0) then Result:= key
         else Result:= #0; //если беззнаковое, или минус не первый, запрещаем
  //все остальные символы запрещаем:
  else Result:= #0;
  end; //case
end;

{проверка правильности символа в вещественном числе}
function TrueFloatKeys(key:char; str:string):char;
begin
  //сначала укажем, что возвращается тот же символ, что ввел пользователь:
  Result:= key;
  //далее делаем проверку на правильность символа. если символ не правильный,
  //мы его запретим:
  case key of
    //все числа разрешаем:
    '0'..'9': ;
    //backspace разрешаем:
    #8: ;
    //если разделителя еще нет - выводим правильный разделитель,
    //иначе ничего не выводим
    ',' , '.' : if Pos(DecimalSeparator, str)= 0 then
                  Result := DecimalSeparator
                else Result := #0;
    //разрешаем минус при условии, что минус - первый символ в строке:
    '-': if Length(str) = 0 then Result:= key
         else Result:= #0; //если минус не первый, запрещаем

  //все остальные символы запрещаем:
  else Result:= #0;
  end; //case
end;

end.
